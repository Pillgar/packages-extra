# Maintainer: Philip MÃ¼ller <philm[at]manjaro[dog]org>

pkgname=calamares
pkgver=3.2.0
_pkgver=3.2.0
pkgrel=4
_commit=
pkgdesc='Distribution-independent installer framework'
arch=('i686' 'x86_64')
license=(GPL)
url="https://github.com/calamares"
license=('LGPL')
depends=('kconfig' 'kcoreaddons' 'kiconthemes' 'ki18n' 'kio' 'solid' 'yaml-cpp' 'kpmcore>=3.3.0' 'mkinitcpio-openswap'
         'boost-libs' 'ckbcomp' 'hwinfo' 'qt5-svg' 'polkit-qt5' 'gtk-update-icon-cache' 'pythonqt>=3.2')
makedepends=('extra-cmake-modules' 'qt5-tools' 'qt5-translations' 'git' 'boost')
backup=('usr/share/calamares/modules/bootloader.conf'
        'usr/share/calamares/modules/displaymanager.conf'
        'usr/share/calamares/modules/initcpio.conf'
        'usr/share/calamares/modules/unpackfs.conf')

source+=("$pkgname-$pkgver.tar.gz::https://github.com/manjaro/calamares/archive/v${_pkgver}.tar.gz"
         "$pkgname-$pkgver-$pkgrel.patch::https://github.com/manjaro/calamares/compare/v3.2.0...3.2.x-stable.patch"
         "${srcdir}/timezone_0.0.png")
         #"$pkgname-$pkgver-$pkgrel.tar.gz::https://github.com/manjaro/calamares/archive/${_commit}.tar.gz")
sha256sums=('fe69f23f3abc0edec729ae236ca21c689099c43ac34d635c8fa2f489146ca2d0'
            '62e1a2093aa5faca1242001ad1f1d89fd43e3b9d75668a0a2ad9ce385040cf1f'
            '852becf04751280e8385a37c9d21c9fa12bc2eb302c56149973e1df74ad84e37')

prepare() {
	#mv ${srcdir}/calamares-${_commit} ${srcdir}/calamares-${_pkgver}
	cd ${srcdir}/calamares-${_pkgver}

	# patches here
	git apply -p1 < ${srcdir}/$pkgname-$pkgver-$pkgrel.patch

	# add revision
	_patchver="$(cat CMakeLists.txt | grep -m3 -e CALAMARES_VERSION_PATCH | grep -o "[[:digit:]]*" | xargs)"
	sed -i -e "s|CALAMARES_VERSION_PATCH $_patchver|CALAMARES_VERSION_PATCH $_patchver-$pkgrel|g" CMakeLists.txt

    # temp workaround for correct display of Iceland's timezone
    # https://github.com/calamares/calamares/issues/967
    cp $srcdir/timezone_0.0.png $srcdir/calamares-$_pkgver/src/modules/locale/images/
}

build() {
	cd ${srcdir}/calamares-${_pkgver}

	mkdir -p build
	cd build
        cmake .. \
              -DCMAKE_BUILD_TYPE=Release \
              -DCMAKE_INSTALL_PREFIX=/usr \
              -DCMAKE_INSTALL_LIBDIR=lib \
              -DWITH_PYTHONQT:BOOL=ON \
              -DSKIP_MODULES="webview interactiveterminal initramfs \
                              initramfscfg dracut dracutlukscfg \
                              dummyprocess dummypython dummycpp \
                              dummypythonqt plasmalnf"
        make
}

package() {
	cd ${srcdir}/calamares-${_pkgver}/build
	make DESTDIR="$pkgdir" install
	install -Dm644 "../data/manjaro-icon.svg" "$pkgdir/usr/share/icons/hicolor/scalable/apps/calamares.svg"
	install -Dm644 "../data/calamares.desktop" "$pkgdir/usr/share/applications/calamares.desktop"
	install -Dm755 "../data/calamares_polkit" "$pkgdir/usr/bin/calamares_polkit"
	install -Dm644 "../data/49-nopasswd-calamares.rules" "$pkgdir/etc/polkit-1/rules.d/49-nopasswd-calamares.rules"
	chmod 750      "$pkgdir"/etc/polkit-1/rules.d
}
