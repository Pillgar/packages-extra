# Maintainer: Stefano Capitani <stefano@manjaro.org>
# Maintainer: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Maintainer: Jan de Groot <jgc@archlinux.org>

pkgbase=gdm
pkgname=(gdm libgdm)
pkgver=3.28.1
pkgrel=1.1
pkgdesc="Display manager and login screen"
url="https://wiki.gnome.org/Projects/GDM"
arch=(x86_64)
license=(GPL)
depends=(gnome-shell gnome-session upower xorg-xrdb xorg-server xorg-server-xwayland xorg-xhost)
makedepends=(yelp-tools intltool gobject-introspection git docbook-xsl)
checkdepends=(check)
_commit=d6199fac252391178026e006c9f4196f88a4b14b  # tags/3.28.1^0
source=("git+https://git.gnome.org/browse/gdm#commit=$_commit"
        0002-Xsession-Don-t-start-ssh-agent-by-default.patch
        gdm.sysusers
        https://github.com/jwrdegoede/gdm/commit/9fe4f09e69a1e2a1b84362f6f94166a8ef891af9.patch
        https://github.com/jwrdegoede/gdm/commit/85e4bdf57c07deae90b6583315403321204da5da.patch
        https://github.com/jwrdegoede/gdm/commit/5c11e2cc780e8e294c3a69d1b758fe8439d14d74.patch
        https://github.com/jwrdegoede/gdm/commit/d69e1f564410ab9c8c38b0506fe831258b4c37f1.patch
        https://github.com/jwrdegoede/gdm/commit/cb1a1cfec990120d89ffcc40fb24ce575ce5c0d5.patch
        https://github.com/jwrdegoede/gdm/commit/b170894d7e1b95eb9dd485cbf051e0d3ca89076b.patch)
sha256sums=('SKIP'
            '9449da0b6ee58aa3fde65e6d8d1f30513e4176a7dc6d176f17f320ce82cb1d82'
            '6d9c8e38c7de85b6ec75e488585b8c451f5d9b4fabd2a42921dc3bfcc4aa3e13'
            '85cb07f05d8830a4cd6bb245c8a33fd5f473c15d4fb7608590a38ebfed963ad2'
            'c53a3fc8e5062a91ccd5fc2f1640f6087a23bf46c2d8b1bd6068bc13f7bf3548'
            '2c7c52a9a33810b31b0ed44e1f84dc889889d586b36a1e473d562b34e7fb626a'
            'b1ba6789e7dc017b344e936dd0ed92bef4d328beaab72e6e8821344337c3ef21'
            'c017b8dc3e02704febed75a43bacbaf7a6a3ad8da0f710b804f9cbbdcac7fbdf'
            '030b39485926e36da386985505366440120867c85f6e89d802970bad8c410588')

pkgver() {
  cd $pkgbase
  git describe --tags | sed 's/-/+/g'
}

prepare() {
  cd $pkgbase
  patch -Np1 -i ../0002-Xsession-Don-t-start-ssh-agent-by-default.patch
  
  # patch to solve the gdm issue see: https://bugzilla.gnome.org/show_bug.cgi?id=747339#c95
  
  patch -Np1 -i ../9fe4f09e69a1e2a1b84362f6f94166a8ef891af9.patch
  patch -Np1 -i ../85e4bdf57c07deae90b6583315403321204da5da.patch 
  patch -Np1 -i ../5c11e2cc780e8e294c3a69d1b758fe8439d14d74.patch 
  patch -Np1 -i ../d69e1f564410ab9c8c38b0506fe831258b4c37f1.patch 
  patch -Np1 -i ../cb1a1cfec990120d89ffcc40fb24ce575ce5c0d5.patch 
  patch -Np1 -i ../b170894d7e1b95eb9dd485cbf051e0d3ca89076b.patch
  
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $pkgbase
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --disable-schemas-compile \
    --disable-static \
    --enable-gdm-xsession \
    --enable-ipv6 \
    --with-default-pam-config=arch \
    --with-default-path=/usr/local/bin:/usr/local/sbin:/usr/bin \
    --without-plymouth \
    --without-tcp-wrappers
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  cd $pkgbase
  make check
}

package_gdm() {
  depends+=(libgdm)
  optdepends=('fprintd: fingerprint authentication')
  backup=(etc/pam.d/gdm-autologin etc/pam.d/gdm-fingerprint etc/pam.d/gdm-launch-environment
          etc/pam.d/gdm-password etc/pam.d/gdm-smartcard etc/gdm/custom.conf
          etc/gdm/Xsession etc/gdm/PostSession/Default etc/gdm/PreSession/Default)
  groups=(gnome)

  cd $pkgbase
  make DESTDIR="$pkgdir" install

  chown -R 120:120 "$pkgdir/var/lib/gdm"

  # Unused or created at start
  rm -r "$pkgdir"/var/{cache,log,run}

  install -Dm644 ../gdm.sysusers "$pkgdir/usr/lib/sysusers.d/gdm.conf"

### Split libgdm
  make -C libgdm DESTDIR="$pkgdir" uninstall
  mv "$pkgdir/usr/share/glib-2.0/schemas/org.gnome.login-screen.gschema.xml" "$srcdir"
}

package_libgdm() {
  pkgdesc="GDM support library"
  depends=(systemd glib2 dconf)

  cd $pkgbase
  make -C libgdm DESTDIR="$pkgdir" install
  install -Dt "$pkgdir/usr/share/glib-2.0/schemas" -m644 \
    "$srcdir/org.gnome.login-screen.gschema.xml"
}
