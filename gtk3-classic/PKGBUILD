# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Tomasz GÄ…sior <tomaszgasior.pl>

# This file is based on original PKGBUILD of GTK3 package.
# https://git.archlinux.org/svntogit/packages.git/plain/trunk/PKGBUILD?h=packages/gtk3

_repo=gtk3-mushrooms
_commit=37ca324e00372ebf9995e38bc363a06338ec974b
__arch_pkg_commit=711373d553d34c1a8c7ff973eaba4dd388c1d9b3

pkgname=gtk3-classic
pkgver=3.22.30
pkgrel=2
pkgdesc="GTK3 patched for classic desktops like MATE/XFCE (see README)."
url="https://github.com/TomaszGasior/gtk3-mushrooms"
_archgit=https://git.archlinux.org/svntogit/packages.git/plain/trunk
conflicts=(gtk3 gtk3-print-backends)
provides=(gtk3=$pkgver gtk3-print-backends)
arch=(x86_64)
license=(LGPL)
install=$pkgname.install

depends=(
    'adwaita-icon-theme'
    'at-spi2-atk'
    'atk'
    'cairo'
    'cantarell-fonts'
    'colord'
    'dconf'
    'desktop-file-utils'
    'gdk-pixbuf2'
    'gtk-update-icon-cache'
    'json-glib'
    'libcanberra'
    'libcups'
    'libepoxy'
    'librsvg'
    'libxcomposite'
    'libxcursor'
    'libxdamage'
    'libxi'
    'libxinerama'
    'libxkbcommon'
    'libxrandr'
    'mesa'
    'pango'
    'rest'
    'shared-mime-info'
    'wayland'
    'wayland-protocols'
)

makedepends=(
    'git'
    'gobject-introspection'
    'gtk-doc'
    'libcanberra'
    'sassc'
)
source=(
    # GTK source code.
    "https://download.gnome.org/sources/gtk+/${pkgver%.*}/gtk+-$pkgver.tar.xz"
    # Temporary. Will be removed with next GTK3 release.
    "upstream_window_background._patch::https://github.com/GNOME/gtk/commit/2ce63a86ba689aa41eb47409c889c469497478b0.patch"
    # ArchLinux package files.
    "settings.ini::$_archgit/settings.ini?h=packages/gtk3&id=$__arch_pkg_commit"
    "gtk-query-immodules-3.0.hook::$_archgit/gtk-query-immodules-3.0.hook?h=packages/gtk3&id=$__arch_pkg_commit"
    # gtk3-mushrooms patches
    "git+$url.git#commit=$_commit"
)
sha256sums=('a1a4a5c12703d4e1ccda28333b87ff462741dc365131fbc94c218ae81d9a6567'
            '98b99ed14cc7545d1f30e244fe793ddd21a7db9d6f5e78b677113a89b621e459'
            '01fc1d81dc82c4a052ac6e25bf9a04e7647267cc3017bc91f9ce3e63e5eb9202'
            'de46e5514ff39a7a65e01e485e874775ab1c0ad20b8e94ada43f4a6af1370845'
            'SKIP')

__patch_makefiles() {
    __replace_string_in_file() {
        sed -i".bak" "s/$1/$2/" "$3"
        rm "$3.bak"
    }

    __replace_string_in_file \
        "SRC_SUBDIRS = gdk gtk libgail-util modules demos tests testsuite examples" \
        "SRC_SUBDIRS = gdk gtk libgail-util modules demos" \
        "Makefile.am"

    __replace_string_in_file \
        "SUBDIRS = po po-properties \$(SRC_SUBDIRS) docs m4macros build" \
        "SUBDIRS = po \$(SRC_SUBDIRS) m4macros build" \
        "Makefile.am"

    __replace_string_in_file \
        "SUBDIRS = gtk-demo widget-factory icon-browser" \
        "SUBDIRS = widget-factory" \
        "demos/Makefile.am"

	__replace_string_in_file \
		"gtk-update-icon-cache" \
		"" \
		"gtk/Makefile.am"
}

__patch_gtk_code() {
    for patch_file in $srcdir/$_repo/*.patch; do
        patch -d "gtk" -p 3 -i "$patch_file"
    done

    cat "$srcdir/$_repo/smaller-adwaita.css" | tee -a gtk/theme/Adwaita/gtk-contained{,-dark}.css > /dev/null
}

prepare() {
    cd "gtk+-$pkgver"

    # Make building faster by skipping tests, code examples and unused elements.
    __patch_makefiles

    # Apply patches to GTK code.
    __patch_gtk_code

    # Temporary patch. Will be removed with next GTK3 release.
    patch -p 1 -i "$srcdir/upstream_window_background._patch"

    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "gtk+-$pkgver"

    CXX=/bin/false ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
      --enable-x11-backend --enable-wayland-backend --enable-broadway-backend \
      --disable-schemas-compile --disable-gtk-doc-html

    # https://bugzilla.gnome.org/show_bug.cgi?id=655517
    sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool

    make -j 15
}

package() {
    cd "gtk+-$pkgver"

    make -j 15 DESTDIR="$pkgdir" install

    install -Dm644 ../settings.ini "$pkgdir/usr/share/gtk-3.0/settings.ini"
    install -Dm644 ../gtk-query-immodules-3.0.hook "$pkgdir/usr/share/libalpm/hooks/gtk-query-immodules-3.0.hook"
}
