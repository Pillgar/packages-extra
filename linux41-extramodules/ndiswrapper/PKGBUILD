# Based on the file created for Arch Linux by:
# Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# SpepS <dreamspepser at yahoo dot it>
# Tobias Powalowski <tpowa@archlinux.org>

# Maintainer: Guinux <nuxgui@gmail.com>
# Co-Maintainer: Clittle <philm@manjarolinux.org>

_pkgname=ndiswrapper
pkgbase=ndiswrapper
pkgname=('linux41-ndiswrapper' 'ndiswrapper-utils')
groups=('linux41-extramodules')
pkgver=1.61
pkgrel=17
pkgdesc="Module for NDIS (Windows Network Drivers) drivers supplied by vendors."
license=('GPL')
arch=('i686' 'x86_64')
url="http://ndiswrapper.sourceforge.net"
makedepends=('linux41-headers' 'linux41')
source=("http://downloads.sourceforge.net/sourceforge/$_pkgname/$_pkgname-$pkgver.tar.gz"
        "kernel-4.0.patch")
options=('!strip')
sha256sums=('2ac1847c24cbfa4f48a800b04c9721219614f1663a4ac94af3c7939b45c47584'
            '80f89025a3de4b1decb82550bd89a3b4e43732b8f5104bac6e2e4b66d20d2d57')

prepare() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np1 -i $srcdir/kernel-4.0.patch
}

build() {
  _extramodules=extramodules-4.1-MANJARO
  _kver="$(cat /usr/lib/modules/${_extramodules}/version)"
  cd "$srcdir/$_pkgname-$pkgver"

  # modinfo path fix
  sed -i "/modinfo/s/s/usr\//" driver/Makefile

  # make sure we point to the right build directory
  sed -i "/^KBUILD/ s,.*,KBUILD = $(readlink -f /usr/lib/modules/$_kver/build)," driver/Makefile

  make KVERS=$_kver
}

package_linux41-ndiswrapper() {
  _extramodules=extramodules-4.1-MANJARO
  _kver="$(cat /usr/lib/modules/${_extramodules}/version)"
  depends=('linux41' "$_pkgname-utils")
  provides=("$_pkgname=$pkgver")
  groups=('linux41-extramodules')
  install=ndiswrapper.install
  cd "$srcdir/$_pkgname-$pkgver"

  make INST_DIR="usr/lib/modules/$_extramodules" \
    KVERS=$_kver DESTDIR="$pkgdir/" sbindir=/usr/bin usrsbindir=/usr/bin install

  # set the kernel we've built for inside the install script
  sed -i -e "s/EXTRAMODULES=.*/EXTRAMODULES=${_extramodules}/g" "${startdir}/${_pkgname}.install"

  gzip "$pkgdir/usr/lib/modules/$_extramodules/$_pkgname.ko"

  # mv ndiswrapper-utils
  mkdir -p $srcdir/tmp-ndis-utils/usr
  mv -v "$pkgdir/usr/share" "$srcdir/tmp-ndis-utils/usr"
  mv -v "$pkgdir/usr/bin" "$srcdir/tmp-ndis-utils/usr"
}

package_ndiswrapper-utils() {
  pkgdesc="Utils for NDIS (Windows Network Drivers) drivers supplied by vendors."
  pkgrel=2
  depends=('wireless_tools' 'perl')
  groups=()
  cd $srcdir/tmp-ndis-utils
  mv -v usr "$pkgdir"
}

# vim:set ts=2 sw=2 et:
