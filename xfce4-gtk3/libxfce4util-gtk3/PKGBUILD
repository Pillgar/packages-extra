# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=libxfce4util
pkgname=$_pkgname-gtk3
pkgver=4.13.2
pkgrel=1
pkgdesc="Basic utility non-GUI functions for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
depends=('glib2')
makedepends=('gobject-introspection' 'xfce4-dev-tools')
conflicts=($_pkgname)
provides=($_pkgname)
_commit=63a62d9904af287636600c7016512366c87a403e
options=(!libtool)
source=("https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2")
sha256sums=('bf214d4b996f860d1fd1925baa0f22051ee3109d67e8c6ebfd4279694dd1cdaa')

build() {
  cd $_pkgname-$_commit

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --sbindir=/usr/bin \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd $_pkgname-$_commit
  make DESTDIR="$pkgdir" install
  rm -rf "$pkgdir"/usr/share/gtk-doc/
}
