# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfce4-notifyd
_commit=f33eb9fa21136876989f7f0465a77c07394235dc
pkgname=$_pkgname-gtk3
pkgver=0.4.2
pkgrel=1
pkgdesc="Notification daemon for the Xfce desktop"
arch=('i686' 'x86_64')
url="http://goodies.xfce.org/projects/applications/xfce4-notifyd"
license=('GPL2')
groups=('xfce4-gtk3-goodies')
depends=('exo-gtk3'
    'hicolor-icon-theme'
    'libnotify'
    'libxfce4ui-gtk3')
makedepends=('intltool' 'python' 'xfce4-dev-tools' 'xfce4-panel-gtk3')
conflicts=("$_pkgname")
provides=("$_pkgname")
source=(https://git.xfce.org/apps/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2)
sha256sums=('bbdff564020c0e1991ae01a9cd5cd06d1356c1064930aedcc67da80e87bea592')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
