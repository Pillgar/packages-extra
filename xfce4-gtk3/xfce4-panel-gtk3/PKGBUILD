# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfce4-panel
pkgname="$_pkgname-gtk3"
pkgver=4.13.3
pkgrel=3
pkgdesc="Panel for the Xfce desktop environment"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('desktop-file-utils'
    'exo-gtk3'
    'garcon-gtk3'
    'hicolor-icon-theme'
    'libwnck3'
    'libxfce4ui-gtk3'
    'vala'
    'xfconf-gtk3>=4.13.3')
makedepends=('intltool' 'xfce4-dev-tools')
_commit=738032483f4501f3743a8cef3e169129b3e8c10c
provides=($_pkgname)
conflicts=($_pkgname)
source=(https://git.xfce.org/xfce/xfce4-panel/snapshot/xfce4-panel-$_commit.tar.bz2)
sha256sums=('cd28767f34826e31a406871b766013e3d470c0e2ca0c49b2a5c140ee86701f23')

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib \
    --localstatedir=/var \
    --disable-static \
    --enable-gio-unix \
    --enable-gtk3 \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
