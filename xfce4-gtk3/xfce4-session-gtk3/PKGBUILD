# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

_pkgname=xfce4-session
pkgname=$_pkgname-gtk3
pkgver=4.13.1
pkgrel=1
pkgdesc="A session manager for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('exo-gtk3'
    'hicolor-icon-theme'
    'libsm'
    'libwnck3'
    'libxfce4ui-gtk3'
    'polkit'
    'polkit-gnome'
    'xorg-iceauth'
    'xorg-xinit'
    'xorg-xrdb')
makedepends=('intltool' 'xfce4-dev-tools')
optdepends=('gnome-keyring: for keyring support when GNOME compatibility is enabled'
            'xscreensaver: for locking screen with xflock4'
            'gnome-screensaver: for locking screen with xflock4'
            'xlockmore: for locking screen with xflock4'
            'slock: for locking screen with xflock4')
replaces=('xfce-utils')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=a8892a866ed29d96e145c44dec88afd560c67cbf
source=("https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2"
        dbus-update-environment.patch
        xfce-polkit-gnome-authentication-agent-1.desktop)
sha256sums=('6a57e57ef42b2a076ef0b5f75b5ca2c8faae0e4dfff8c8843ff8a737e830f138'
            '9158404ba903b20b32f366a19cef7627eaf385ee145abbb3e8b1b459b6604cc3'
            '74c94c5f7893d714e04ec7d8b8520c978a5748757a0cdcf5128492f09f31b643')

prepare() {
  cd "$srcdir/$_pkgname-$_commit"

  # Support using existing dbus-daemon
  patch -Np1 -i ../dbus-update-environment.patch
}

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --libexecdir=/usr/lib/xfce4 \
    --localstatedir=/var \
    --disable-static \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install

  # Provide a default PolicyKit Authentication Agent (FS#42569)
  install -d "$pkgdir/etc/xdg/autostart"
  cp "$srcdir/xfce-polkit-gnome-authentication-agent-1.desktop" \
    "$pkgdir/etc/xdg/autostart/"
}
