# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>
# Contributor: Corrado Primier <bardo@aur.archlinux.org>

_pkgname=xfce4-settings
pkgname=$_pkgname-gtk3
pkgver=4.13.4
pkgrel=1
pkgdesc="Settings manager for xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('exo-gtk3' 'garcon-gtk3' 'libxfce4ui-gtk3' 'libnotify' 'libxklavier'
         'gnome-icon-theme' 'gnome-themes-standard')
makedepends=('intltool' 'xf86-input-libinput' 'xfce4-dev-tools')
optdepends=('libcanberra: for sound control')
provides=($_pkgname)
conflicts=($_pkgname)
_commit=54c36351043c90acfe23b06dd23216f94816e0df
source=(https://git.xfce.org/xfce/$_pkgname/snapshot/$_pkgname-$_commit.tar.bz2
        default-xsettings-xml.patch)
sha256sums=('e017a851d2ddfc1d15825cfb452af26d0c61dc72068070a566eda3c9a24355af'
            '1c4110fd5ce0f17564cda892b72d59a87b515fdb4580c7154bbb4c1979f5ade7')
prepare() {
  cd "$srcdir/$_pkgname-$_commit"

  # Enable GNOME icon theme, Adwaita theme and font hinting by default
  patch -Np1 -i "$srcdir/default-xsettings-xml.patch"
}

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --localstatedir=/var \
    --disable-static \
    --enable-xrandr \
    --enable-xcursor \
    --enable-libnotify \
    --enable-libxklavier \
    --enable-pluggable-dialogs \
    --enable-sound-settings \
    --disable-upower-glib \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
  # workaround as xfsettingsd crashes on SX-SF
  mv "$pkgdir"/usr/bin/xfsettingsd "$pkgdir"/usr/bin/xfce4-settings
  echo "#!/bin/bash" > "$pkgdir"/usr/bin/xfsettingsd
  echo "export XFSETTINGSD_DEBUG=1" >> "$pkgdir"/usr/bin/xfsettingsd
  echo "xfce4-settings --replace --no-daemon >> /tmp/xfsettingsd.log 2>&1" >> "$pkgdir"/usr/bin/xfsettingsd
  chmod a+x "$pkgdir"/usr/bin/xfsettingsd
}
