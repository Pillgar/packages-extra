# Maintainer: Bernhard Landauer <oberon@manjaro.org>
# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: tobias <tobias funnychar archlinux.org>

pkgname=xfdesktop-gtk3
_pkgname=xfdesktop
pkgver=4.13.2
pkgrel=3
pkgdesc="A desktop manager for Xfce"
arch=('i686' 'x86_64')
url="http://www.xfce.org/"
license=('GPL2')
groups=('xfce4-gtk3')
depends=('exo-gtk3'
    'garcon-gtk3'
    'hicolor-icon-theme'
    'libwnck3'
    'libxfce4ui-gtk3'
    'thunar-gtk3')
makedepends=('intltool' 'xfce4-dev-tools')
conflicts=('xfce4-menueditor' "$_pkgname")
provides=("$_pkgname")
replaces=('xfce4-menueditor')
_commit=1c466085b2e5aa4c2c5d48fd52c50f085ff3f685
source=(https://git.xfce.org/xfce/xfdesktop/snapshot/xfdesktop-$_commit.tar.gz)
sha256sums=('b849c9cd704f8791adb783fb4dcfa7df5b3fcc512fc676d7348ffc00d621ee59')

prepare() {
  cd "$srcdir/$_pkgname-$_commit"
}

build() {
  cd "$srcdir/$_pkgname-$_commit"

  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --enable-gio-unix \
    --enable-thunarx \
    --enable-notifications \
    --disable-debug
  make
}

package() {
  cd "$srcdir/$_pkgname-$_commit"
  make DESTDIR="$pkgdir" install
}
